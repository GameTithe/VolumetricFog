#include "/Engine/Public/Platform.ush"

Texture2D<float2> VelocityInput;
Texture2D<float> PressureInput;
RWTexture2D<float2> VelocityOutput;

int2 Resolution;
float HalfInvDx;    // 0.5 / dx ( dx = 1.0 / Resoution, 0.5 * Resolution)

[numthreads(8,8,1)]
void MainCS(uint3 DTid : SV_DispatchThreadID)
{
    if (any(DTid.xy >= (uint2) Resolution))
    {
        return;
    }
    
    int2 Pos = int2(DTid.xy);
    
    float Left = PressureInput[clamp(Pos + int2(-1, 0), int2(0, 0), Resolution - 1)];
    float Right = PressureInput[clamp(Pos + int2(1, 0), int2(0, 0), Resolution - 1)];
    float Bottom = PressureInput[clamp(Pos + int2(0, 1), int2(0, 0), Resolution - 1)];
    float Top = PressureInput[clamp(Pos + int2(0, -1), int2(0, 0), Resolution - 1)];

    float2 GradP = float2(Right - Left, Top - Bottom) * HalfInvDx;
    
    float2 Vel = VelocityInput[Pos];
    Vel -= GradP;
    
    VelocityOutput[DTid.xy] = Vel;
}

